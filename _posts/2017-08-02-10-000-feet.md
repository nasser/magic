---
layout: post
title: 10,000 Feet
private: true
---

The best way to start understanding MAGIC is with a 10,000 foot view of its architecture.

Generally speaking, a compiler's job is to convert code in one language into another.
In particular, user-facing compilers convert source text that the user wrote into runnable code in some target environment.
For the most part, MAGIC is no different.
It takes as input Clojure source and outputs bytecode to be executed on the CLR.

What *is* different is that MAGIC is a *functional* compiler, meaning it does not depend on anything stateful.
Every phase takes persistent data as input and (up to the final one) produces persistent data as output.
The phases are decoupled and decomplected, making the whole thing easier to work on and reason about.

The following diagram describes the whole of MAGIC.
Each boxed node represents a phase of the compiler, arrows represent the flow of data, and the labels on the arrows represent the kind of data flowing between two nodes.

<div class="mermaid">
graph TD
Reader -- s-expressions --> Analyzer
Analyzer -- abstract syntax tree --> Compiler
Compiler -- symbolic bytecode --> Emitter
Emitter -- bytecode --> CLR
</div>

## Reader

In Lisps, the reader's job is to convert source text into s-expressions, the *code as data* representation of source code that the whole language rests on. 
In Clojure's case, this means lists, vectors, maps, symbols, and keywords, among others.
MAGIC uses ClojureCLR's reader without modification, which allows MAGIC to run alongside the stock compiler without issue.
The function that implements this phase is `clojure.core/read-string`.

```clojure
user> (-> "(+ 1 2)" read-string)
(+ 1 2)
user> (-> "(+ 1 2)" read-string type)
clojure.lang.PersistentList
user> (-> "(+ 1 2)" read-string first)
+
user> (-> "(+ 1 2)" read-string first type)
clojure.lang.Symbol
```

Since the stock reader is being used, I almost never explicitly call `read-string` and use quoted forms instead.

ClojureCLR's reader differs from ClojureJVM's in that it supports the pipe character `|` to parse symbols that would otherwise include invalid characters.

```clojure
user> (-> "Foo[1]" read-string)
Foo
user> (-> "Foo[1]" read-string type)
clojure.lang.Symbol
user> (-> "Foo|[1]|" read-string)
Foo[1]
user> (-> "Foo|[1]|" read-string type)
clojure.lang.Symbol
user> (-> "|Foo[1]|" read-string)
Foo[1]
user> (-> "|Foo[1]|" read-string type)
clojure.lang.Symbol
```

David Miller [added this to ClojureCLR's reader](https://github.com/clojure/clojure-clr/wiki/Specifying-types) to be able to specify CLR types, which often contain spaces and square brackets.
MAGIC makes use of this reader feature as well.

## Analyzer
The analyzer's job is to produce an AST from an s-expression.
If an s-expression can be thought of as *what the user wrote*, then an AST is *what the user meant*.
It enforces Clojure's semantics, including the flow of types, special forms, local symbol binding, and interoperation with the CLR.
The function that implements this phase is `magic.analyzer/analyze`.

MAGIC's analyzer is built on Nicola Mometto's excellent `tools.analyzer` library.
It represents ASTs as Clojure hash-maps and is extendable by scheduling additional *analysis passes*.
MAGIC adds passes for CLR interop and additional optimizations.

As an example, this is what the analysis of `(str 2)` looks like.

```clojure
user> (-> '(str 2) magic.analyzer/analyze pprint)
{:op :invoke,
 :form (str 2),
 :env {:context :ctx/expr, :locals {}, :ns user},
 :fn
 {:op :var,
  :assignable? false,
  :var #'clojure.core/str,
  :meta
  {:added "1.0",
   :ns #object[Namespace 0xc6a4a506 "clojure.core"],
   :name str,
   :file "clojure/core.clj",
   :static true,
   :column 2,
   :line 543,
   :tag System.String,
   :arglists ([] [x] [x & ys]),
   :doc
   "With no args, returns the empty string. With one arg x, returns\n  x.toString().  (str nil) returns the empty string. With more than\n  one arg, returns the concatenation of the str values of the args."},
  :env {:context :ctx/expr, :locals {}, :ns user},
  :form str},
 :args
 [{:op :const,
   :env {:context :ctx/expr, :locals {}, :ns user},
   :type :number,
   :literal? true,
   :val 2,
   :form 2}],
 :children [:fn :args],
 :top-level true}
 ```
 
It is a hash map describing the meaning of `(str 2)`.
The `:op` key indicates the kind of AST node this is, in this case `:invoke`, and the `:children` key indicates which keys represent nested AST nodes, in this case `:fn` and `:args`.
The `:fn` key maps to another AST node, with its own `:op`, that describes what is being invoked, in this case the var `#'clojure.core/str`.
`:args` maps to a vector of AST nodes of the arguments, in this case the literal `2`.
Other keys encode other information that might be useful to further passes or compiler phases.

It recognized the symbol `str` as the Clojure var `#'clojure.core/str`, following Clojure's symbol resolution rules.
But `str` does not always refer to that var.
We could, for example, *shadow* the symbol `str` in a let binding, and the analyzer would produce a different AST node.

```clojure
user> (-> '(let [str 1] str) magic.analyzer/analyze pprint)
{:op :let,
 :body
 {:op :do,
  :ret
  {:op :local,
   :name str__#0,
   :form str,
   :local :let,
   :children [:init],
   :assignable? false,
   ;; ...
   :init
   {:op :const,
    :env {:context :ctx/expr, :locals {}, :ns user},
    :type :number,
    :literal? true,
    :val 1,
    :form 1}},
  :children [:statements :ret],
  :body? true},
 ;; ...
 }
 ```
 
Here, the `str` in the return position of the let binding can be found in the `:ret` of the `:body` of the let binding's AST node.
Despite being the same symbol as the `str` in the previous example, *this* AST node is a `:local`, initialized to the literal value 1, because the analyzer enforced Clojure's symbol shadowing rules.
This is what I mean by the difference between what the user *said* versus what the user *meant*.

## Compiler
At the heart of MAGIC is the actual compiler.
It takes an AST node and produces *symbolic bytecode* that represents the instructions on the CLR required to execute that expression.
The function that implements this phase is `magic.core/compile`.

### Symbolic Bytecode
MAGIC depends on another library: MAGE.
MAGE provides a persistent data representation of CLR bytecode, the so-called "symbolic bytecode", and a mechanism to produce actual CLR bytecode from that representation.
Because symbolic bytecode is represented as Clojure data, it is very amenable to functional programming and REPL-driven development.
In fact, MAGE was the first part of the compiler built, and none of MAGIC would be possible without it.

If the ASTs represent what the user meant, symbolic bytecode represents *how to do that on the CLR*.
As an example, this is the bytecode that executes `(str 1)`.

```clojure
user> (-> '(str 1) magic.analyzer/analyze magic.core/compile pprint)
[[[{:mage.core/opcode ldstr, :mage.core/argument "clojure.core"}
   {:mage.core/opcode ldstr, :mage.core/argument "str"}
   {:mage.core/opcode call,
    :mage.core/argument
    #object[MonoMethod 0xb69ce8b8 "clojure.lang.Var var(System.String, System.String)"]}]
  {:mage.core/opcode call,
   :mage.core/argument
   #object[MonoMethod 0x4faf2845 "System.Object getRawRoot()"]}]
 [{:mage.core/opcode castclass, :mage.core/argument clojure.lang.IFn}
  ({:mage.core/opcode ldc.i8, :mage.core/argument 1}
   {:mage.core/opcode box, :mage.core/argument System.Int64})
  {:mage.core/opcode callvirt,
   :mage.core/argument
   #object[MonoMethod 0xc9589b79 "System.Object invoke(System.Object)"]}
  {:mage.core/opcode castclass, :mage.core/argument System.String}]]
```

The CLR is a stack machine, so instructions execute against an invisible value stack, pushing and popping to the top at all times.
The nested vectors do not mean anything to the compiler, and are a result of internal functions returning vectors of symbolic bytecode.

The way to read this is:

1. Load the string "clojure.core" to the stack (`ldstr`)
2. Load the string "str" to the stack (`ldstr`)
3. Call the static method `var` passing in the above two strings as arguments, returning a `clojure.lang.Var` (`call`)
4. Call the instance method `getRawRoot` on the var, returning an `Object` (`call`)
5. Cast the result to `clojure.lang.IFn` (`castclass`)
6. Load a long constant with a value of 1 (`ldc.i8`)
7. Convert the long constant to an `Object` (`box`)
8. Call the instance method `invoke` on the `IFn` from step 5 passing the `Object` from step 7 as a parameter, returning an `Object` (`callvirt`)
9. Cast the result to a string (`castclass`)

It takes an awful lot to do anything interesting at the level of bytecode!

MAGIC isn't a monolithic compiler, either.
Each `:op` value an AST has its *own compiler* that generates a body of bytecode to implement just that expresison.
This is possible because of the symbolic nature of MAGE bytecode â€“Â its just data in this phase.
In the above example, the compiler for `:const` generated the bytecode to load a long 1 (the `ldc.i8` opcode) and the compiler for `:invoke` generated the rest.

The ability to talk about bits of bytecode abstractly, independent of where they will end up, is a seachange in my experience working on compilers.
MAGIC also allows compilers to be switched out as needed with closures, which allows normally complicated expressions like `let` and `loop` to be implemented very tersely and without depending on external state.

## Emitter
Finally, the emitter (MAGE) takes symbolic bytecode and generates useable CLR bytecode, completing the process.
The function that implements this phase is `mage.core/emit!`.

The semantics of the CLR make it difficult to emit anything smaller than a type, so emitting the bytecode for `(str 1)` on its own would not work.
Clojure functions are represented as types on the CLR (and the JVM, for that matter), so the level of granularity for MAGE's `emit!` is usually a Clojure function.

```clojure
user> (-> '(fn [a b] (str a "~" b))
          magic.analyzer/analyze
          magic.core/compile
          mage.core/emit!
          :mage.core/type-builder)
magic$$user$$--anonymous--$$8009
user> (import magic$$user$$--anonymous--$$8009)
magic$$user$$--anonymous--$$8009
user> (def tilde-join (magic$$user$$--anonymous--$$8009.))                                               #'user/tilde-join
user> (tilde-join :hello :world)
":hello~:world"
```

We read, analyze, compile, and emit a function which side effects the virtual machine to create a new type, `magic$$user$$--anonymous--$$8009` in this case. `emit!` returns a large hash map containing its context (not shown here), and we grab the new type using the `:mage.core/type-builder` key.
We import it, assign an instance of it to a Clojure var, and invoke normally.

And that's the whole compiler!

ðŸŽ©âœ¨

[opcodes]: https://msdn.microsoft.com/en-us/library/system.reflection.emit.opcodes_fields(v=vs.110).aspx

*[AST]: Abstract Syntax Tree
*[MAGE]: Morgan And Grand Emitter
*[MSIL]: Microsoft Intermediate Language
*[CLR]: Common Language Runtime
*[JVM]: Java Virtual Machine